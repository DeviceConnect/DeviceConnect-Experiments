apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    useLibrary 'org.apache.http.legacy'

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags --long"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags --long"].execute().in.text.trim()
        }
        version = version.empty ? "2.0.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId 'org.deviceconnect.android.deviceplugin.slackmessagehook'
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName getVersionName()
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }
    productFlavors {
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

repositories {
    maven { url 'https://raw.githubusercontent.com/DeviceConnect/DeviceConnect-Android/master/dConnectSDK/dConnectSDKForAndroid/repository/' }
    maven { url 'https://raw.githubusercontent.com/DeviceConnect/DeviceConnect-Android/master/dConnectDevicePlugin/dConnectDevicePluginSDK/repository/' }
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'org.deviceconnect:dconnect-device-plugin-sdk:2.5.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
}
def distRootDir = '../dist'
def distDir = distRootDir + '/dConnectDeviceSlackMessageHook'
task cleanDist(type: Delete) {
    delete distRootDir
}
task assembleDistRelease(dependsOn: ['assembleRelease', 'copyRelease', 'copySource']) {
}
task assembleDistDebug(dependsOn: ['assembleDebug', 'copyDebug', 'copySource']) {
}
task copyRelease(type: Copy) {
    from 'build/outputs/apk'
    into distDir
    include '**/slack-plugin-release.apk'
    rename 'slack-plugin-release.apk', 'dConnectDeviceSlackMessageHook-release.apk'
}
task copyDebug(type: Copy) {
    from 'build/outputs/apk'
    into distDir
    include '**/slack-plugin-debug.apk'
    rename 'slack-plugin-debug.apk', 'dConnectDeviceSlackMessageHook-debug.apk'
}
task copySource(type: Copy) {
    from '../'
    into distDir + '/src/dConnectDeviceSlackMessageHook'
    exclude '**/build/**/*'
    exclude '**/dist/**/*'
    exclude '**/gradle.properties'
    exclude '**/local.properties'
    exclude '**/*.apk'
    includeEmptyDirs false
    rename 'gradle-default.properties', 'gradle.properties'
}